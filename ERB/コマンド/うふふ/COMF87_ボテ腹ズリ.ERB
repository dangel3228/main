;-------------------------------------------------
;ボテ腹ズリ
;奉仕系コマンド
;-------------------------------------------------
@COMNAME87
#FUNCTIONS
SELECTCASE COMMODE()
CASE 1
	TSTR:コマンド名受渡 = ボテ腹ズリ
CASE 2
	TSTR:コマンド名受渡 = ダブルボテ腹ズリ
ENDSELECT
;複数モード_ダミーをリセット
複数モード_ダミー = 0
 
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_87
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
同時モード許可 = 1

@COM_TOOLTIP_87
LOCALS = <br>■ボテ腹ズリ<br>
LOCALS += "相手に妊娠して大きくなった腹部で自分のペニスを愛撫してもらうコマンド。<br>ボテ腹ズリモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｓ・露出・接触・奉仕・不潔"
TSTR:ツールチップ受渡 = %LOCALS%

@COM87
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("ボテ腹ズリ")
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕快楽
TFLAG:30 = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 10
DOWNBASE:気力 += 100

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 100)
SIF COMMODE() == 2
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 150)
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 400)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 150)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 += 2
RETURN 1

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
@CAN_COM87(ARG)
SKIPDISP ARG
PRINTL ボテ腹ズリ

CALL COM_ORDER
;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINT 奉仕精神
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 3
	PRINT 精液中毒
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,5) * 1
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 4
	PRINT 恥じらい
	PRINTV '(,4,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF S
		PRINT  + 
	A += 1
	PRINT 汚臭鈍感
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	A -= 2
	PRINT 汚臭敏感
	PRINTV '(,2,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 8
	PRINT 献身的
	PRINTV '(,8,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 12
	PRINT 男嫌い
	PRINTV '(,12,')
	S = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF S
		PRINT  + 
	A += 5
	PRINT 親愛
	PRINTV '(,5,')
	S = 5
ENDIF

;合計を表示(25以上で実行)
PRINT  = 
PRINTV A

V = 25
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE87
;ボテ腹ズリ実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(87)
	RETURN RESULT
;ある程度の妊娠進行度が必要
SIF PREGNANCY_SIZE(TARGET) < 2
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0
;上半身着衣
;鎧、差し込めないorはだけない服があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:はだけ可)
	RETURN 0
;ペニスを何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM87
#DIMS 腹部描写
SELECTCASE PREGNANCY_SIZE(TARGET)
	CASE IS >= 5
		腹部描写 = 巨大にせり出したボテ腹
	CASE 4
		腹部描写 = 大きく膨らんだボテ腹
	CASEELSE
		腹部描写 = 山なりに盛り上がった腹部
ENDSELECT
IF COMMODE() == 2
	PRINTFORML %CALLNAME:TARGET%は%腹部描写%を%CALLNAME:助手は誰だ()%の腹部とぴったりくっつけて、協力してボテ腹でペニスを扱いている…
ELSE
	PRINTFORML %CALLNAME:TARGET%は%腹部描写%を押し付けて、のしかかるようにペニスに腹部を擦り付けている…
ENDIF