@SHOW_USERCOM
TFLAG:100 = 1
VARSET LOCAL
IF TCVAR:MASTER:112 == 1
	LOCAL:1 = MASTER_POSE(6,1,1)
ELSEIF TCVAR:MASTER:112 == 2
	LOCAL:1 = MASTER_POSE(7,1,1)
ENDIF

;コマンド表示
CALL SHOW_COMMAND

SIF !TFLAG:100
	RETURN 0
RESETCOLOR
PRINTC 能力表示(マスター)[804]
PRINTC コマンドフィルタ[809]
IF CFLAG:うふふ
	PRINTC 服を脱ぐ[820]
	IF CFLAG:うふふ == 2
		PRINTC 抜け出す[999]
	ELSE
		PRINTC 逢引終了[999]
	ENDIF
ENDIF
IF CFLAG:飲み会
	PRINTC 飲み会終了[998]
ENDIF
PRINTC 探索パーティ表示[830]
SIF !CFLAG:TARGET:同行 && !CFLAG:MASTER:隠密
	PRINTC 物陰に隠れる[840]
SIF CFLAG:TARGET:同行 && !CFLAG:MASTER:隠密
	PRINTC 一緒に隠れる[840]
SIF CFLAG:MASTER:隠密 && (!CFLAG:うふふ || GET_TARGETNUM() < 2)
	PRINTC 物陰から出る[841]
SIF TARGET
	PRINTC アイテムを使う[850]
PRINTC 所持指輪一覧[851]
PRINTC 所持耳飾り一覧[852]
PRINTC debug用居場所表示[860]

PRINTL 
PRINT ＜コマンド履歴:
FOR LOCAL, 0, 5
	IF コマンド履歴:LOCAL >= 370 && コマンド履歴:LOCAL <= 374
		SIF LOCAL > 0
			PRINT  - 
		;現在位置が同じかつCOMABLEが通るならボタン化
		IF TARGET == コマンド履歴:(LOCAL + 5)
			TRYCALLFORM COM_ABLE{コマンド履歴:LOCAL}
			IF RESULT
				PRINTBUTTON @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]", コマンド履歴:LOCAL
			ELSE
				PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			ENDIF
		ELSE
			;一旦TARGETが違うなら一旦相手を保存してから固有コマンド表示
			LOCAL:1 = TARGET
			TARGET = コマンド履歴:(LOCAL + 5)
			PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			TARGET = LOCAL:1
		ENDIF
	ELSEIF コマンド履歴:LOCAL >= 380 && コマンド履歴:LOCAL <= 384
		SIF LOCAL > 0
			PRINT  - 
		;現在位置が同じかつCOMABLEが通るならボタン化
		IF CFLAG:MASTER:現在位置 == コマンド履歴:(LOCAL + 10) && CFLAG:MASTER:現在マップ種別 == コマンド履歴:(LOCAL + 15)
			TRYCALLFORM COM_ABLE{コマンド履歴:LOCAL}
			IF RESULT
				PRINTBUTTON @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]", コマンド履歴:LOCAL
			ELSE
				PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			ENDIF
		ELSE
			;一旦現在位置が違うなら一旦位置を保存してから固有コマンド表示
			LOCAL:1 = CFLAG:MASTER:現在位置
			LOCAL:2 = CFLAG:MASTER:現在マップ種別
			CFLAG:MASTER:現在位置 = コマンド履歴:(LOCAL + 10)
			PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			CFLAG:MASTER:現在位置 = LOCAL:1
			CFLAG:MASTER:現在マップ種別 = LOCAL:2
		ENDIF
	ELSEIF コマンド履歴:LOCAL >= 0
		SIF LOCAL > 0
			PRINT  - 
		;COMABLEが通るならボタン化
		TRYCALLFORM COM_ABLE{コマンド履歴:LOCAL}
		IF RESULT
			PRINTBUTTON @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]", コマンド履歴:LOCAL
		ELSE
			PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
		ENDIF
	ELSEIF コマンド履歴:LOCAL == -1
		SIF LOCAL > 0
			PRINT  - 
		;COMABLEが通るならボタン化
		TRYCALLFORM COM_ABLE_S%派生コマンド履歴:LOCAL%
		IF RESULT
			PRINTBUTTON @"[%TRAIN_COMNAME_S(派生コマンド履歴:LOCAL)%]", @"%派生コマンド履歴:LOCAL%"
		ELSE
			PRINTFORM [%TRAIN_COMNAME_S(派生コマンド履歴:LOCAL)%]
		ENDIF
	ENDIF
NEXT
PRINTL ＞

@USERCOM
#DIM ページ番号
#DIM 数値変換用
#DIM TARGET何人目？

ページ番号 = 0
;RETURN 0でSOURCE_CHECK以降に進む
;NEXTCOMを仕込む
IF NEXTCOM >= 0
	RESULT = NEXTCOM
	NEXTCOM = -1
ENDIF

IF TSTR:SELECTCOM_S受け渡し != ""
	;受け渡しがある場合はRESULTSに派生コマンドが入っているはず
	SELECTCOM = -1
	SELECTCOM_S = %RESULTS%
	TRYCALLFORM COM_ABLE_S%SELECTCOM_S%
	SIF RESULT == 0 || RESULT == -1
		RETURN 1
	SIF TRAIN_COMNAME_S(SELECTCOM_S) == ""
		RETURN 1
	TSTR:SELECTCOM_S受け渡し = %SELECTCOM_S%
	RESULT = 1
	LOCAL:2 = 0
	TRYCALLFORM CAN_COM_S%SELECTCOM_S%(1)
	SIF !RESULT
		SETBIT LOCAL:2, 0
	IF COMMODE() == 2
		LOCAL:1 = TARGET
		TARGET = 助手は誰だ()
		RESULT = 1
		TRYCALLFORM CAN_COM_S%SELECTCOM_S%(1)
		SIF !RESULT
			SETBIT LOCAL:2, 1
		TARGET = LOCAL:1
	ENDIF
	IF LOCAL:2
		SIF GETBIT(LOCAL:2, 0)
			TRYCALLFORM CAN_COM_S%SELECTCOM_S%
		IF GETBIT(LOCAL:2, 1)
			LOCAL:1 = TARGET
			TARGET = 助手は誰だ()
			TRYCALLFORM CAN_COM_S%SELECTCOM_S%
			TARGET = LOCAL:1
		ENDIF
		RETURN 1
	ENDIF
ELSEIF RESULT >= 9220 && RESULT <= 9233
	LOCAL = RESULT
	IF MODE_確認("Ａ挿入系", TARGET)
		SELECTCASE LOCAL
			CASE 9220
				A挿入強さ:TARGET:0 = MAX(A挿入強さ:TARGET:0 - 1, 0)
			CASE 9201 TO 9205
				A挿入強さ:TARGET:0 = LOCAL - 9221
			CASE 9226
				A挿入強さ:TARGET:0 = MIN(A挿入強さ:TARGET:0 + 1, 4)
			CASE 9227
				A挿入速さ:TARGET:0 = MAX(A挿入速さ:TARGET:0 - 1, 0)
			CASE 9228 TO 9232
				A挿入速さ:TARGET:0 = LOCAL - 9228
			CASE 9233
				A挿入速さ:TARGET:0 = MIN(A挿入速さ:TARGET:0 + 1, 4)
		ENDSELECT
	ENDIF
	IF MODE_確認("Ａ同時挿入", TARGET)
		SELECTCASE LOCAL
			CASE 9220
				A挿入強さ:(助手は誰だ()):0 = MAX(A挿入強さ:(助手は誰だ()):0 - 1, 0)
			CASE 9201 TO 9205
				A挿入強さ:(助手は誰だ()):0 = LOCAL - 9221
			CASE 9226
				A挿入強さ:(助手は誰だ()):0 = MIN(A挿入強さ:(助手は誰だ()):0 + 1, 4)
			CASE 9227
				A挿入速さ:(助手は誰だ()):0 = MAX(A挿入速さ:(助手は誰だ()):0 - 1, 0)
			CASE 9228 TO 9232
				A挿入速さ:(助手は誰だ()):0 = LOCAL - 9228
			CASE 9233
				A挿入速さ:(助手は誰だ()):0 = MIN(A挿入速さ:(助手は誰だ()):0 + 1, 4)
		ENDSELECT
	ENDIF
	RETURN 1
ELSEIF RESULT >= 9200 && RESULT <= 9213
	LOCAL = RESULT
	IF MODE_確認("Ｖ挿入系", TARGET)
		SELECTCASE LOCAL
			CASE 9200
				V挿入強さ:TARGET:0 = MAX(V挿入強さ:TARGET:0 - 1, 0)
			CASE 9201 TO 9205
				V挿入強さ:TARGET:0 = LOCAL - 9201
			CASE 9206
				V挿入強さ:TARGET:0 = MIN(V挿入強さ:TARGET:0 + 1, 4)
			CASE 9207
				V挿入速さ:TARGET:0 = MAX(V挿入速さ:TARGET:0 - 1, 0)
			CASE 9208 TO 9212
				V挿入速さ:TARGET:0 = LOCAL - 9208
			CASE 9213
				V挿入速さ:TARGET:0 = MIN(V挿入速さ:TARGET:0 + 1, 4)
		ENDSELECT
	ENDIF
	IF MODE_確認("同時挿入", TARGET)
		SELECTCASE LOCAL
			CASE 9200
				V挿入強さ:(助手は誰だ()):0 = MAX(V挿入強さ:(助手は誰だ()):0 - 1, 0)
			CASE 9201 TO 9205
				V挿入強さ:(助手は誰だ()):0 = LOCAL - 9201
			CASE 9206
				V挿入強さ:(助手は誰だ()):0 = MIN(V挿入強さ:(助手は誰だ()):0 + 1, 4)
			CASE 9207
				V挿入速さ:(助手は誰だ()):0 = MAX(V挿入速さ:(助手は誰だ()):0 - 1, 0)
			CASE 9208 TO 9212
				V挿入速さ:(助手は誰だ()):0 = LOCAL - 9208
			CASE 9213
				V挿入速さ:(助手は誰だ()):0 = MIN(V挿入速さ:(助手は誰だ()):0 + 1, 4)
		ENDSELECT
	ENDIF
	RETURN 1
ELSEIF RESULT > 9100 && RESULT <= 9105
;9101-9105はモード変更用
	SIF !CFLAG:うふふ || !助手は誰だ()
	;3P中でないならなにもしない
		RETURN 1
	IF RESULT == 9101
		FOR LOCAL, 0, 3
			CLEARBIT FLAG:モード管理, LOCAL
		NEXT
	ELSEIF GROUPMATCH(RESULT, 9102, 9103, 9104)
		LOCAL:1 = RESULT - 9102
		FOR LOCAL, 0, 3
			IF LOCAL == LOCAL:1
				SETBIT FLAG:モード管理, LOCAL
			ELSE
				CLEARBIT FLAG:モード管理, LOCAL
			ENDIF
		NEXT
	ELSE
		SIF GETBIT(FLAG:モード管理, 2)
			INVERTBIT FLAG:モード管理, 3
	ENDIF
	RETURN 1
ELSEIF RESULT == 9100
;9100でINFO格納フィルタ
	INVERTBIT INFO格納フィルタ:(RESULT - 9100), 0
	RETURN 1
ELSEIF RESULT >=9000
;9000番代は簡易コマンドフィルタに使う
	INVERTBIT 簡易コマンドフィルタ:(RESULT - 9000), 0
	RETURN 1
ELSEIF RESULT >=2000
;2000番以降は相手変えのショートカットに使う
	IF CFLAG:MASTER:隠密 && CFLAG:TARGET:隠密
		PRINTW 一緒に隠密中は相手を変更出来ません
		RETURN 1
	ENDIF
	数値変換用 = RESULT - 2000
	IF 数値変換用 <= 0 || 数値変換用 >= CHARANUM
		RETURN 1
	ELSEIF CFLAG:数値変換用:同室
		TFLAG:104 = 数値変換用
	PREVCOM = RESULT
	ENDIF
	RETURN 1
ELSEIF RESULT >= 0 && RESULT <= 700
	IF RESULT == 351
		CALL COM_ABLE351
		SIF !RESULT
			RETURN 0
		PRINTL 連れ出す
		STR:0 = 連れ出す
		CALL COM351
		RETURN RESULT
	ENDIF
	SELECTCOM = RESULT
	TRYCALLFORM COM_ABLE{SELECTCOM}
	SIF RESULT == 0
		RETURN 1
	SIF TRAIN_COMNAME(SELECTCOM) == ""
		RETURN 1
	TFLAG:SELECTCOM受け渡し = SELECTCOM
	RESULT = 1
	LOCAL:2 = 0
	TRYCALLFORM CAN_COM{SELECTCOM}(1)
	SIF !RESULT
		SETBIT LOCAL:2, 0
	IF COMMODE() == 2
		LOCAL:1 = TARGET
		TARGET = 助手は誰だ()
		RESULT = 1
		TRYCALLFORM CAN_COM{SELECTCOM}(1)
		SIF !RESULT
			SETBIT LOCAL:2, 1
		TARGET = LOCAL:1
	ENDIF
	IF LOCAL:2
		SIF GETBIT(LOCAL:2, 0)
			TRYCALLFORM CAN_COM{SELECTCOM}
		IF GETBIT(LOCAL:2, 1)
			LOCAL:1 = TARGET
			TARGET = 助手は誰だ()
			TRYCALLFORM CAN_COM{SELECTCOM}
			TARGET = LOCAL:1
		ENDIF
		RETURN 1
	ENDIF


ELSEIF RESULT == 803
	FOR LOCAL, 1, CHARANUM
		IF TARGET:LOCAL == TARGET
			TARGET何人目？ = LOCAL
			BREAK
		ENDIF
	NEXT
	$能力表示_LOOP
	LOCAL:1 = 0
	SIF TARGET何人目？ != 1
		SETBIT LOCAL:1, 0
	SIF TARGET:(TARGET何人目？ + 1) > 0
		SETBIT LOCAL:1, 1
	CALL PRINT_STATE(TARGET:TARGET何人目？, LOCAL:1)
	IF RESULT == 100
		TARGET何人目？ -= 1
		GOTO 能力表示_LOOP
	ELSEIF RESULT == 101
		TARGET何人目？ += 1
		GOTO 能力表示_LOOP
	ENDIF
	RETURN 1
ELSEIF RESULT == 804
	CALL PRINT_STATE(MASTER)
	RETURN 1
ELSEIF RESULT == 809
	CALL OPTION_COMMAND_FILTER
	RETURN 1
ELSEIF RESULT == 820 && CFLAG:うふふ
	CALL CLOTHES_SETTING_TRAIN(PLAYER)
	RETURN 1
ELSEIF RESULT == 830
	CALL SHOW_PARTYMEMBER
	RETURN 1
ELSEIF RESULT == 840
	CFLAG:MASTER:隠密 = 1
	SIF CFLAG:TARGET:同行
		CFLAG:TARGET:隠密 = 1
	RETURN 1
ELSEIF RESULT == 841
	CFLAG:MASTER:隠密 = 0
	SIF CFLAG:TARGET:同行
		CFLAG:TARGET:隠密 = 0
	RETURN 1
ELSEIF RESULT == 850
	CALL ITEM_PROTO
	RETURN 1
ELSEIF RESULT == 851
	DO
	LOCALS = 
	LOCALS += "<div rect='32px,210px,906px,600px' border='5px' bcolor='#C0C0C0'></div>"
	CALL 所持指輪リスト表示(0, ページ番号)
	HTML_PRINT LOCALS, 1
	LOCALS = 
	;戻るボタンと前次ページボタン
	LOCALS += "<div rect='42px,780px,886px,580px'>"
	IF ページ番号 > 0
		LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
	ELSE
		LOCALS += "　　　　　　　　　　"
	ENDIF
	IF 表示用ステータス格納配列:30:0 > 0
		LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
	ELSE
		LOCALS += "　　　　　　　　　　"
	ENDIF
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
	LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"
	HTML_PRINT LOCALS, 1
	FOR LOCAL, 0, 45
		PRINTL
	NEXT
	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 9999
			VARSET 表示用ステータス格納配列:0:0
			ページ番号 = 0
			RETURN 1
		CASE IS < 500
			TRYCALLFORM 指輪個別文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名")%
			LOCALS = %詳細文文字列受け渡し変数%
			TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名")%(LOCAL)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			HTML_PRINT LOCALS
			WAIT
		CASE IS < 1000
			TRYCALLFORM 指輪個別文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL - 500, "指輪名")%
			LOCALS = %詳細文文字列受け渡し変数%
			TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL - 500, "指輪名")%(LOCAL - 500)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			HTML_PRINT LOCALS
			WAIT
	ENDSELECT
	LOOP 1
ELSEIF RESULT == 852
	DO
	LOCALS = 
	LOCALS += "<div rect='32px,210px,906px,600px' border='5px' bcolor='#C0C0C0'></div>"
	HTML_PRINT LOCALS, 1
	CALL 所持耳飾りリスト表示(0, ページ番号)
	LOCALS = 
	;戻るボタンと前次ページボタン
	LOCALS += "<div rect='42px,780px,886px,580px'>"
	IF ページ番号 > 0
		LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
	ELSE
		LOCALS += "　　　　　　　　　　"
	ENDIF
	IF 表示用ステータス格納配列:30:0 > 0
		LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
	ELSE
		LOCALS += "　　　　　　　　　　"
	ENDIF
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
	LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"
	HTML_PRINT LOCALS, 1
	FOR LOCAL, 0, 45
		PRINTL
	NEXT
	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 9999
			VARSET 表示用ステータス格納配列:0:0
			ページ番号 = 0
			RETURN 1
		CASE IS < 500
			TRYCALLFORM 耳飾り個別文章表示_%DT_CELL_GETS("所持耳飾りデータベース", LOCAL, "耳飾り名")%
			LOCALS = %詳細文文字列受け渡し変数%
			TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", LOCAL, "耳飾り名")%(LOCAL)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			HTML_PRINT LOCALS
			WAIT
		CASE IS < 1000
			TRYCALLFORM 耳飾り個別文章表示_%DT_CELL_GETS("所持耳飾りデータベース", LOCAL - 500, "耳飾り名")%
			LOCALS = %詳細文文字列受け渡し変数%
			TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", LOCAL - 500, "耳飾り名")%(LOCAL - 500)
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			HTML_PRINT LOCALS
			WAIT
	ENDSELECT
	LOOP 1
ELSEIF RESULT == 860
	CALL OPTION_DEBUG
	RETURN 1
ELSEIF RESULT == 888
	FLAG:一日終了 = 1
ELSEIF RESULT == 999 && CFLAG:うふふ
	;IF CFLAG:(TARGET:1):うふふ == 2 && BASE:(TARGET:1):満足 < MAXBASE:(TARGET:1):満足
	;	PRINTFORMW %CALLNAME:(TARGET:1)%が離してくれない…
	;	CALL KOJO_EVENT, 13, TARGET, 1
	;	RETURN 1
	;ELSE
	;	CALL KOJO_EVENT, 13, TARGET, 0
	;ENDIF
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:滞在期間 < 0
			CONTINUE
		CFLAG:LOCAL:うふふ = 0
		;モードもリセット
		VARSET 体位モード格納:LOCAL:0
		VARSET モード主犯:LOCAL:0
	NEXT
	;同時モードもリセット
	FLAG:モード管理 = 0
	RETURN 1
ELSEIF RESULT == 998 && CFLAG:飲み会
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:滞在期間 < 0
			CONTINUE
		CFLAG:LOCAL:飲み会 = 0
		;先にここでモードを変えておかないとTARGETが上手く格納されない
		TFLAG:調教モード = 1
		;モードもリセット
		VARSET 体位モード格納:LOCAL:0
		VARSET モード主犯:LOCAL:0
	NEXT
	RETURN 1
ELSE
	RETURN 1
ENDIF
RETURN 0

@SHOW_COMMAND
CALL SHOW_MODE(TARGET:1)
CALL MODE詳細設定処理(TARGET:1)
IF TARGET:2
	CALL SHOW_MODE(TARGET:2)
	CALL MODE詳細設定処理(TARGET:1)
ENDIF

IF TFLAG:調教モード == 2
	SETCOLOR 0xC0C0C0
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTPLAIN [通常]
	PRINTL -------------------------------------------------------------------------------------------------
	CALL SHOW_COMMAND_実処理("その他")
PRINTL
	SETCOLOR 0xFF66CC
	SIF 簡易コマンドフィルタ:6
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[Ｖ系]", 9006
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:6
		CALL SHOW_COMMAND_実処理("Ｖ系")
PRINTL
	SETCOLOR 0xDDFFDD
	SIF 簡易コマンドフィルタ:7
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[Ａ系]", 9007
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:7
		CALL SHOW_COMMAND_実処理("Ａ系")
PRINTL
	SETCOLOR 0xD3DEF1
	SIF 簡易コマンドフィルタ:9
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[道具系]", 9009
	PRINTL -----------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:9
		CALL SHOW_COMMAND_実処理("道具系")
PRINTL
	SETCOLOR 0xD3DEF1
	SIF 簡易コマンドフィルタ:12
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[複数人プレイ]", 9012
	PRINTL -----------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:12
		CALL SHOW_COMMAND_実処理("複数人プレイ")
ELSEIF TFLAG:調教モード == 3
	SETCOLOR 0xC0C0C0
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTPLAIN [飲み会]
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:49
		CALL SHOW_COMMAND_実処理("その他")
PRINTL
	SETCOLOR 0xFF66CC
	SIF 簡易コマンドフィルタ:6
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[セクハラ]", 9006
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:1
		CALL SHOW_COMMAND_実処理("セクハラ")
ELSE
	SETCOLOR 0xC0C0C0
	SIF 簡易コマンドフィルタ:0
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[日常]", 9000
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:0
		CALL SHOW_COMMAND_実処理("日常")
PRINTL
	SETCOLOR 0xFF99EE
	SIF 簡易コマンドフィルタ:1
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[セクハラ]", 9001
	PRINTL ---------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:1
		CALL SHOW_COMMAND_実処理("セクハラ")
PRINTL
	SETCOLOR 0xAADDAA
	SIF 簡易コマンドフィルタ:4
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[探索]", 9004
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:4
		CALL SHOW_COMMAND_実処理("探索")
PRINTL
	SETCOLOR 0xB0BBE0
	SIF 簡易コマンドフィルタ:49
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[その他]", 9049
	PRINTL -----------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:49
		CALL SHOW_COMMAND_実処理("その他")
ENDIF
RESETCOLOR
PRINTL
DRAWLINE
文字色保存 = 0

@SHOW_COMMAND_実処理(ARGS)
#DIMS 複合条件保存, 10
TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000

VARSET LOCALS
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = COMTYPE(ARGS)
;セクハラは全部まとめてセクハラ扱い
SIF GROUPMATCH(LOCAL:3, 1, 2, 3)
	LOCAL:3 = 1
SIF ARGS == "その他"
	LOCAL:3 = 49

FOR LOCAL,0,700
	;助手モードはいったんPLAYERを助手にする
	IF GETBIT(FLAG:モード管理, 1)
		LOCAL:10 = PLAYER
		PLAYER = 助手は誰だ()
	ENDIF
	TRYCCALLFORM COM_ABLE{LOCAL}
		;助手モードのときに変えたPLAYERを元に戻す
		SIF GETBIT(FLAG:モード管理, 1)
			PLAYER = LOCAL:10
		SIF RESULT == 0
			CONTINUE
	CATCH
		;CATCHでも元に戻す
		SIF GETBIT(FLAG:モード管理, 1)
			PLAYER = LOCAL:10
		CONTINUE
	ENDCATCH
	;同時モードはいったんTARGETを助手と入替えてもう一度チェック
	IF GETBIT(FLAG:モード管理, 0)
		LOCAL:10 = TARGET
		TARGET = 助手は誰だ()
		CALLFORM COM_ABLE{LOCAL}
		TARGET = LOCAL:10
		SIF RESULT == 0
			CONTINUE
	ENDIF
		弱点コマンドカテゴリ = 
	TRYCALLFORMF COMTYPE_{LOCAL}
	;複合条件処理
	IF TFLAG:コマンドタイプ受渡 > 99
		IF TFLAG:コマンドタイプ受渡 == 100
			SPLIT TSTR:複合コマンドタイプ受け渡し, "&", 複合条件保存
		ELSEIF TFLAG:コマンドタイプ受渡 == 101
			SPLIT TSTR:複合コマンドタイプ受け渡し, "_", 複合条件保存
		ENDIF
		FOR LOCAL:1, 0, RESULT
			IF TFLAG:調教モード == 2
				IF 複合条件保存:(LOCAL:1) == "複数人プレイ"
					TFLAG:コマンドタイプ受渡 = 12
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "Ｖ系"
					TFLAG:コマンドタイプ受渡 = 6
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "Ａ系"
					TFLAG:コマンドタイプ受渡 = 7
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "道具系"
					TFLAG:コマンドタイプ受渡 = 9
					BREAK
				ENDIF
			ELSEIF TFLAG:調教モード == 3
				IF GROUPMATCH(複合条件保存:(LOCAL:1), "セクハラ弱", "セクハラ", "セクハラ強")
					TFLAG:コマンドタイプ受渡 = 1
					BREAK
				ENDIF
			ELSE
				IF 複合条件保存:(LOCAL:1) == "日常"
					TFLAG:コマンドタイプ受渡 = 0
					BREAK
				ELSEIF GROUPMATCH(複合条件保存:(LOCAL:1), "セクハラ弱", "セクハラ", "セクハラ強")
					TFLAG:コマンドタイプ受渡 = 1
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "探索"
					TFLAG:コマンドタイプ受渡 = 4
					BREAK
				ENDIF
			ENDIF
		NEXT
	ENDIF
	;指定カテゴリと同じコマンドだけ表示
	SELECTCASE LOCAL:3
		CASE 0 TO 48
			IF GROUPMATCH(TFLAG:コマンドタイプ受渡, 1, 2, 3)
				SIF LOCAL:3 != 1
					CONTINUE
			ELSEIF TFLAG:コマンドタイプ受渡 != LOCAL:3
				CONTINUE
			ENDIF
		CASE 49
			SIF GROUPMATCH(TFLAG:コマンドタイプ受渡, 0, 1, 2, 3, 4, 6, 7, 9, 12)
				CONTINUE
			SIF 簡易コマンドフィルタ:49
				CONTINUE
	ENDSELECT
	SIF 簡易コマンドフィルタ処理(LOCAL) == 1
		CONTINUE
	SELECTCASE LOCAL
		CASE 370 TO 371
			LOCALS:1 = {LOCAL}_{NO:TARGET}
		CASE 380 TO 384
			LOCALS:1 = {LOCAL}_{CFLAG:MASTER:現在マップ種別}_{CFLAG:MASTER:現在位置}
		CASEELSE
			LOCALS:1 = {LOCAL}
	ENDSELECT
	IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
		LOCALS:1 = %SUBSTRING(弱点コマンドカテゴリ, 12)%
	ENDIF
	FOR LOCAL:10, 0, 5
		SIF 弱点看破:TARGET:(LOCAL:10) == 0
			CONTINUE
		SIF 弱点コマンドカテゴリ == ""
			CONTINUE
		IF 弱点コマンド枠:TARGET:(LOCAL:10) == LOCALS:1
			LOCALS += "<font color='#FF3399'>"
			LOCAL:4 = 1
			BREAK
		ENDIF
	NEXT
	IF STRLENS(TRAIN_COMNAME(LOCAL))
		LOCALS += @"<button value = '{LOCAL}'"
		TRYCCALLFORM COM_TOOLTIP_{LOCAL}
			IF ツールチップ切り替えOPTION記憶 == 0
				LOCALS += @" title = '%TSTR:ツールチップ受渡%'>"
			ELSE
				LOCALS += @">"
			ENDIF
		CATCH
			LOCALS += @">"
		ENDCATCH
		LOCALS += @"%TRAIN_COMNAME(LOCAL), 22%[{LOCAL, 3}]</button>"
		IF LOCAL:4
			LOCALS += "</font>"
			LOCAL:4 = 0
		ENDIF
		LOCAL:2 += 1
	ENDIF
	IF LOCAL:2 >= 4
		LOCAL:2 = 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT
IF LOCALS != ""
	LOCAL:2 = 0
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF

FOR LOCAL, 0, 500
	LOCAL:2 = 0
	SIF LOCAL >= 370 && LOCAL <= 390
		CONTINUE
	FOR LOCAL:1, 1, 50
		TRYCCALLFORM COM_ABLE_S{LOCAL}_{LOCAL:1}
			SIF RESULT == 0
				CONTINUE
		CATCH
			CONTINUE
		ENDCATCH
		RESULT = 0
		弱点コマンドカテゴリ = 
		TRYCALLFORMF COMTYPE_S_{LOCAL}_{LOCAL:1}
		;複合条件処理
		IF TFLAG:コマンドタイプ受渡 > 99
			IF TFLAG:コマンドタイプ受渡 == 100
				SPLIT TSTR:複合コマンドタイプ受け渡し, "&", 複合条件保存
			ELSEIF TFLAG:コマンドタイプ受渡 == 101
				SPLIT TSTR:複合コマンドタイプ受け渡し, "_", 複合条件保存
			ENDIF
			FOR LOCAL:10, 0, RESULT
				IF TFLAG:調教モード == 2
					IF 複合条件保存:(LOCAL:10) == "複数人プレイ"
						TFLAG:コマンドタイプ受渡 = 12
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "Ｖ系"
						TFLAG:コマンドタイプ受渡 = 6
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "Ａ系"
						TFLAG:コマンドタイプ受渡 = 7
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "道具系"
						TFLAG:コマンドタイプ受渡 = 9
						BREAK
					ENDIF
				ELSE
					IF 複合条件保存:(LOCAL:10) == "日常"
						TFLAG:コマンドタイプ受渡 = 0
						BREAK
					ELSEIF GROUPMATCH(複合条件保存:(LOCAL:10), "セクハラ弱", "セクハラ", "セクハラ強")
						TFLAG:コマンドタイプ受渡 = 1
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "探索"
						TFLAG:コマンドタイプ受渡 = 4
						BREAK
					ENDIF
				ENDIF
			NEXT
		ENDIF
		;指定カテゴリと同じコマンドだけ表示
		SELECTCASE LOCAL:3
			CASE 0 TO 48
				IF GROUPMATCH(TFLAG:コマンドタイプ受渡, 1, 2, 3)
					SIF LOCAL:3 != 1
						CONTINUE
				ELSEIF TFLAG:コマンドタイプ受渡 != LOCAL:3
					CONTINUE
				ENDIF
			CASE 49
				SIF GROUPMATCH(TFLAG:コマンドタイプ受渡, 0, 1, 2, 3, 4, 6, 7, 9, 12)
					CONTINUE
				SIF 簡易コマンドフィルタ:49
					CONTINUE
		ENDSELECT
		IF LOCAL:2 == 0
			PRINTL
			PRINTFORML ▼%TRAIN_COMNAME(LOCAL)%派生
		ENDIF
		LOCALS:1 = {LOCAL}_{LOCAL:1}
		SIF 簡易コマンドフィルタ処理(LOCAL) == 1
			CONTINUE
		IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
			LOCALS:1 = %SUBSTRING(弱点コマンドカテゴリ, 12)%
		ENDIF
		FOR LOCAL:10, 0, 5
			SIF 弱点看破:TARGET:(LOCAL:10) == 0
				CONTINUE
			SIF 弱点コマンドカテゴリ == ""
				CONTINUE
			IF 弱点コマンド枠:TARGET:(LOCAL:10) == @"%LOCALS:1%"
				LOCALS += "<font color='#FF3399'>"
				LOCAL:4 = 1
				BREAK
			ENDIF
		NEXT
		LOCALS += @"<button value = '{LOCAL}_{LOCAL:1}'"
		TRYCCALLFORM COM_TOOLTIP_S{LOCAL}_{LOCAL:1}
			IF ツールチップ切り替えOPTION記憶 == 0
				LOCALS += @" title = '%TSTR:ツールチップ受渡%'>"
			ELSE
				LOCALS += @">"
			ENDIF
		CATCH
			LOCALS += @">"
		ENDCATCH
		LOCALS += @"%TRAIN_COMNAME_S(LOCALS:1), 25%[%LOCALS:1,5%]</button>"
		IF LOCAL:4
			LOCALS += "</font>"
			LOCAL:4 = 0
		ENDIF
		LOCAL:2 += 1
		SETCOLOR 文字色保存
		IF LOCAL:2 >= 4
			LOCAL:2 = 0
			HTML_PRINT LOCALS
			LOCALS = 
		ENDIF
		; PRINTBUTTONC @"%TRAIN_COMNAME_S(LOCALS)%[%LOCALS,5%]", @"%LOCALS%"
		; SETCOLOR 文字色保存
		; LOCAL:2 = 1
	NEXT
	IF LOCALS != ""
		LOCAL:2 = 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT



@ITEM_PROTO
$選択肢ループラベル
VARSET LOCAL
DRAWLINE
IF ITEM:発情誘発剤 < 1
	SETCOLOR 0x666666
	PRINTFORML ■発情誘発剤を所持していません。
	RESETCOLOR
ELSEIF 陥落チェック(TARGET) == 0 && 知識素質:TARGET:性知識 != -2
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:TARGET%が無知でも陥落状態でもないため■発情誘発剤は使用出来ません。
	RESETCOLOR
ELSEIF CFLAG:TARGET:発情期フラグ < 0
	SETCOLOR 0x666666
	PRINTFORML 既に発情中のため■発情誘発剤は使用出来ません。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■発情誘発剤 × {ITEM:発情誘発剤}", 103
	PRINTL 
ENDIF
DRAWLINE

IF ITEM:危険日変動薬 < 1
	SETCOLOR 0x666666
	PRINTFORML ■危険日変動薬を所持していません。
	RESETCOLOR
ELSEIF 陥落チェック(TARGET) == 0 && 知識素質:TARGET:性知識 != -2
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:TARGET%が無知でも陥落状態でもないため■危険日変動薬は使用出来ません。
	RESETCOLOR
ELSEIF RISKY_DAY(TARGET) == 2
	SETCOLOR 0x666666
	PRINTFORML 既に今日は%CALLNAME:TARGET%の一番の危険日なので■危険日変動薬を使用する必要はありません。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■危険日変動薬 × {ITEM:危険日変動薬}", 104
	PRINTL 
ENDIF
DRAWLINE

IF ITEM:避妊薬 < 1
	SETCOLOR 0x666666
	PRINTFORML ■避妊薬を所持していません。
	RESETCOLOR
ELSEIF TCVAR:TARGET:避妊薬
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:TARGET%は既に■避妊薬を服用中です。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■避妊薬 × {ITEM:避妊薬}", 92
	PRINTL 
ENDIF
DRAWLINE
IF ITEM:排卵誘発薬 < 1
	SETCOLOR 0x666666
	PRINTFORML ■排卵誘発薬を所持していません。
	RESETCOLOR
ELSEIF 陥落チェック(TARGET) == 0 && 知識素質:TARGET:性知識 != -2
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:TARGET%が無知でも陥落状態でもないため■排卵誘発薬は使用出来ません。
	RESETCOLOR
ELSEIF TCVAR:TARGET:避妊薬
	SETCOLOR 0x666666
	PRINTFORML 避妊薬を服用中のため■排卵誘発薬を使用出来ません。
	RESETCOLOR
ELSEIF TCVAR:TARGET:排卵誘発薬
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:TARGET%は既に■排卵誘発薬を服用中です。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■排卵誘発薬 × {ITEM:排卵誘発薬}", 91
	PRINTL 
ENDIF
DRAWLINE

IF ITEM:好感度上昇用仮アイテム > 0
	PRINTBUTTON @"■好感度上昇用仮アイテム × {ITEM:好感度上昇用仮アイテム}", 100
	PRINTL 
ELSE
	SETCOLOR 0x666666
	PRINTFORML ■好感度上昇用仮アイテムを所持していません。
	RESETCOLOR
ENDIF
LOCAL:2 = 恋慕枠判定()
LOCAL:1 = -1
FOR LOCAL, 0, LOCAL:2
	IF 恋慕対象フラグ:LOCAL == TARGET
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1 > 0
	IF ITEM:恋慕上昇用仮アイテム > 0
		PRINTBUTTON @"■恋慕上昇用仮アイテム × {ITEM:恋慕上昇用仮アイテム}", 101
		PRINTL 
	ELSE
		SETCOLOR 0x666666
		PRINTFORML ■恋慕上昇用仮アイテムを所持していません。
		RESETCOLOR
	ENDIF
ENDIF
PRINTL ※プロトタイプ開発中はバランス未調整のため、好感度と恋慕度を直接増加させるアイテムを仮実装しています。
PRINTL 
PRINTBUTTON "[999]戻る", 999
BINPUT

SELECTCASE RESULT:0
	CASE 91
		PRINTFORMW %CALLNAME:TARGET%が排卵誘発薬を服用しました。
		TCVAR:TARGET:排卵誘発薬 = 1
		ITEM:排卵誘発薬 -= 1
	CASE 92
		PRINTFORMW %CALLNAME:TARGET%が避妊薬を服用しました。
		TCVAR:TARGET:避妊薬 = 1
		TCVAR:TARGET:排卵誘発薬 = 0
		ITEM:避妊薬 -= 1
	CASE 100
		IF ITEM:100 > 0
			PRINTFORML %CALLNAME:TARGET%の好感度が上昇しました。
			PRINTL 好感度＋10000
			CFLAG:TARGET:友情度 += 10000
			CALL FAVOR_LvUPCheck(TARGET)
			ITEM:100 -= 1
		ELSE
			PRINTW 対象アイテムがありません。
		ENDIF
		GOTO 選択肢ループラベル
	CASE 101
		IF LOCAL:1 > 0
			IF ITEM:101 > 0
				PRINTFORML %CALLNAME:TARGET%の恋慕度が上昇しました。
				PRINTL 恋慕度＋5000
				CFLAG:TARGET:恋慕度 += 5000
				CALL Love_LvUPCheck(TARGET)
				ITEM:101 -= 1
			ELSE
				PRINTW 対象アイテムがありません。
			ENDIF
		GOTO 選択肢ループラベル
		ENDIF
	CASE 103
		PRINTFORMW %CALLNAME:TARGET%が発情期に入りました。
		CFLAG:TARGET:発情期フラグ = -1
		ITEM:103 -= 1
		IF (TALENT:種族 == 3 || NO:TARGET == 11) && 素質切り替えOPTION記憶:母乳体質_発情期_なし == 0
			SIF TALENT:母乳体質 == 0
				TALENT:母乳体質 = 2
		ENDIF
	CASE 104
		PRINTFORMW %CALLNAME:TARGET%の一番妊娠しやすい日が今日に変更されました。
		CFLAG:TARGET:危険日 = DAY % 10
		SIF CFLAG:TARGET:危険日 == 0
			CFLAG:TARGET:危険日 = 10
		ITEM:104 -= 1
	CASE 999
		RETURN
	CASEELSE
		GOTO 選択肢ループラベル
ENDSELECT


